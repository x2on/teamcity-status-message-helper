buildscript {
   repositories {
      mavenCentral()
   }
   dependencies {
      classpath 'com.github.townsfolk:gradle-release:1.2'
   }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'release'

group = 'de.felixschulze.teamcity'

ext.archivesBaseName = 'teamcity-status-message-helper'
ext.isSnapshot = version.endsWith("-SNAPSHOT")

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

signing {
    sign configurations.archives
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar(type: Jar) {
    description = 'An archive of the JavaDoc for Maven Central'
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourceJar
}

// Only *require* signing if we are uploading a non snapshot version
gradle.taskGraph.whenReady { taskGraph ->
    tasks.withType(org.gradle.plugins.signing.Sign).all {
        required = taskGraph.hasTask(":uploadArchives") && !isSnapshot
    }
}


install.repositories.mavenInstaller {
    pom.project(pomConfiguration)
    if (signing.signatory) {
        beforeDeployment { signing.signPom(it) }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        if (signing.signatory) {
            beforeDeployment { signPom(it) }
        }
        name = 'mavenCentralReleaseDeployer'
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: System.properties['sonatypeUsername'], password: System.properties['sonatypePassword'])
            releases(updatePolicy: 'always')
            snapshots(updatePolicy: 'always')
        }
        pom.project(pomConfiguration)
    }
}

release {
	createReleaseTag.dependsOn uploadArchives
  	failOnUnversionedFiles = false
}


/**v
 * Create POM config and return for use by other tasks.
 */
def getPomConfiguration() {
    return {
        name 'TeamCity status message helper'
        packaging 'jar'
        description 'A helper for using status message api from TeamCity.'
        url 'https://github.com/x2on/teamcity-status-message-helper'
        licenses {
            license {
                name 'The MIT License'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'x2on'
                name 'Felix Schulze'
                email 'code@felixschulze.de'
            }
        }
        scm {
            connection 'scm:https://x2on@github.com/x2on/teamcity-status-message-helper'
            developerConnection 'scm:git@github.com:x2on/teamcity-status-message-helper.git'
            url 'https://github.com/x2on/teamcity-status-message-helper'
        }
    }
}